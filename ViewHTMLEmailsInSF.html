%%[

SET @SubId = RequestParameter("SubId")
SET @JobId = RequestParameter("JobId")
SET @BatchId = RequestParameter("BatchId")

/* First HTML Email We'll try to retrieve */

IF NOT EMPTY(@SubId) AND NOT EMPTY(@JobId) AND NOT EMPTY(@BatchId) THEN
    SET @HTMLEmail = Lookup("MSR_Operations_DE_HTMLSendLog_20240617", "HTMLEmail", "SubID", @SubId, "JobID", @JobId, "BatchId", @BatchId, 'HTMLRendered', 'true')


    /* Second HTML Email We'll try to retrieve from the SendLog */
    IF EMPTY(@HTMLEmail) THEN
      SET @View_Email_URL = Lookup("SendLog", "view_email_url", "SubID", @SubId, "JobID", @JobId, "BatchId", @BatchId)

      IF NOT EMPTY(@View_Email_URL) THEN
        SET @HTMLWebEmail = HTTPGet(@View_Email_URL)
      
        IF INDEXOF(@HTMLWebEmail, "This link has expired") == 0 AND INDEXOF(@HTMLWebEmail, "The system is temporarily unavailable") == 0 THEN
          SET @Disclaimer = "Please note that this version of the email is based on real-time data and therefore may not accurately reflect the email the user received at the time of send."
          SET @HTMLEmail = @HTMLWebEmail
        ENDIF
      ENDIF
    ENDIF

    /* Cleaning Up HTML to remove links and track email open snippet */
    IF NOT EMPTY(@HTMLEmail) THEN
      SET @HTMLEmail = Replace(@HTMLEmail, "href=", "")
    ELSE 
      SET @Disclaimer = "Unfortunately, we're not able to show a version of this email. Please reach out to the Marketing Automation team for more details."  
    ENDIF


ELSE
  SET @Disclaimer = "Please make sure to add SubId, JobId and BatchId as parameters in order to preview the email that the user received"
ENDIF

]%%

<!DOCTYPE html>
<html lang="en" dir="ltr">
  <head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
    <title>MSR SFMC Custom Survey Login</title>
    <link rel='stylesheet' href='https://cdn.jsdelivr.net/npm/bootstrap@5.0.0-beta2/dist/css/bootstrap.min.css'>
    <link rel="stylesheet" href="https://mc44xj67dmqjm3p127xlr44g8ttq.pub.sfmc-content.com/y3lreusoo1q">
    <link rel="icon" href="https://www.msrenewal.com/favicon-32x32.png">
  </head>

  <body>
    %%[IF NOT EMPTY(@Disclaimer) THEN]%%
    <div class="container-fluid pt-4 pb-2">
      <div class="row d-flex justify-content-center">
          <div class="col-10 text-center" style="max-width:800px;">
            <span style="font-weight:bold;color:red;text-align:center;font-size:22px;">
              %%=v(@Disclaimer)=%%</span>         
          </div>
      </div>
    </div>
    %%[ ENDIF ]%%
    <div class="container-fluid pt-4">
        <div class="row d-flex justify-content-center">
            <div class="col-12">
                %%=v(@HTMLEmail)=%%          
            </div>
        </div>
    </div>
    <!--End of Email Preview -->

  </body>
</html>
